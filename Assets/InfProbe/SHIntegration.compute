#pragma kernel CSMain


#define MAP_SIZE 64
#define COFF_COUNT 9


TextureCube<float3> TexEnv;
SamplerState samplerTexEnv{
    Filter = MIN_MAG_MIP_LINEAR;
    AddressU = Wrap;
    AddressV = Wrap;
    AddressW = Wrap;
};

RWBuffer<float> BufCoeff;


groupshared float3 BufTmpCoeffAcc[MAP_SIZE][COFF_COUNT];


void projectOntoSH(in float3 n, in float3 color, out float3 sh[COFF_COUNT]){
    const float z2 = n.z * n.z;

    // l=0
    const float p_0_0 = (0.282094791773878140f);
    sh[0] = color * (p_0_0); // l=0,m=0
    // l=1
    const float p_1_0 = (0.488602511902919920f) * n.z;
    sh[2] = color * (p_1_0); // l=1,m=0
    // l=2
    const float p_2_0 = (0.946174695757560080f) * z2 + (-0.315391565252520050f);
    sh[6] = color * (p_2_0); // l=2,m=0


    const float s1 = n.y;
    const float c1 = n.x;

    // l=1
    const float p_1_1 = (-0.488602511902919920f);
    sh[1] = color * (p_1_1 * s1); // l=1,m=-1
    sh[3] = color * (p_1_1 * c1); // l=1,m=+1

    // l=2
    const float p_2_1 = (-1.092548430592079200f) * n.z;
    sh[5] = color * (p_2_1 * s1); // l=2,m=-1
    sh[7] = color * (p_2_1 * c1); // l=2,m=+1


    const float s2 = n.x * s1 + n.y * c1;
    const float c2 = n.x * c1 - n.y * s1;

    // l=2
    const float p_2_2 = (0.546274215296039590f);
    sh[4] = color * (p_2_2 * s2); // l=2,m=-2
    sh[8] = color * (p_2_2 * c2); // l=2,m=+2
}


[numthreads(MAP_SIZE, 1, 1)]
void CSMain(
    uint3 uGroupID : SV_GroupID,
    uint3 uDispatchThreadID : SV_DispatchThreadID,
    uint3 uGroupThreadID : SV_GroupThreadID,
    uint uGuroupIndex : SV_GroupIndex
)
{
    // cube map coordinate based on SHMath of DirectXMath
    float3 vCubeCoord;
    [flatten]
    switch(uDispatchThreadID.z){
    case 0: // positive X
        vCubeCoord.z = 1.f - (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        vCubeCoord.y = 1.f - (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.x = 1.f;
        break;
    case 1: // negative X
        vCubeCoord.z = -1.f + (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        vCubeCoord.y = 1.f - (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.x = -1.f;
        break;

    case 2: // positive Y
        vCubeCoord.z = -1.f + (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.y = 1.f;
        vCubeCoord.x = -1.f + (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        break;
    case 3: // negative Y
        vCubeCoord.z = 1.f - (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.y = -1.f;
        vCubeCoord.x = -1.f + (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        break;

    case 4: // positive Z
        vCubeCoord.z = 1.f;
        vCubeCoord.y = 1.f - (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.x = -1.f + (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        break;
    case 5: // negative Z
        vCubeCoord.z = -1.f;
        vCubeCoord.y = 1.f - (2.f * float(uDispatchThreadID.y) + 1.f) / float(MAP_SIZE);
        vCubeCoord.x = 1.f - (2.f * float(uDispatchThreadID.x) + 1.f) / float(MAP_SIZE);
        break;

    default: // error
        vCubeCoord.x = 0.f;
        vCubeCoord.y = 0.f;
        vCubeCoord.z = 0.f;
        break;
    }
    vCubeCoord = normalize(vCubeCoord);

    const float fB = -1.f + 1.f / float(MAP_SIZE);
    const float fS = (2.f * (1.f - 1.f / float(MAP_SIZE)) / (float(MAP_SIZE) - 1.f));
    const float3 vSolidUV = float3(float(uDispatchThreadID.x) * fS + fB, float(uDispatchThreadID.y) * fS + fB, 1.f);
    const float fSolidDot = dot(vSolidUV, vSolidUV);
    const float fSolidDotSqrtInv = rsqrt(fSolidDot);

    const float fDiffSolid = (4.f / fSolidDot) * fSolidDotSqrtInv;

    float3 vColor = TexEnv.SampleLevel(samplerTexEnv, vCubeCoord, 0);
    vColor *= fDiffSolid;

    float3 vSH[COFF_COUNT];
    projectOntoSH(vCubeCoord, vColor, vSH);

    [unroll]
    for(uint i = 0; i < COFF_COUNT; ++i)
        BufTmpCoeffAcc[uGroupThreadID.x][i] = vSH[i];
    GroupMemoryBarrierWithGroupSync();

    [unroll(MAP_SIZE)]
    for(uint j = MAP_SIZE >> 1; j > 0; j >>= 1){
        if(uGroupThreadID.x < j){
            [unroll]
            for(uint i = 0; i < COFF_COUNT; ++i)
                BufTmpCoeffAcc[uGroupThreadID.x][i] += BufTmpCoeffAcc[uGroupThreadID.x + j][i];
        }
        GroupMemoryBarrierWithGroupSync();
    }

    if(uGroupThreadID.x == 0){
        [unroll]
        for(uint i = 0; i < COFF_COUNT; ++i){
            BufCoeff[uGroupID.z * MAP_SIZE * COFF_COUNT * 3 + uGroupID.y * COFF_COUNT * 3 + i * 3 + 0] = BufTmpCoeffAcc[0][i].x;
            BufCoeff[uGroupID.z * MAP_SIZE * COFF_COUNT * 3 + uGroupID.y * COFF_COUNT * 3 + i * 3 + 1] = BufTmpCoeffAcc[0][i].y;
            BufCoeff[uGroupID.z * MAP_SIZE * COFF_COUNT * 3 + uGroupID.y * COFF_COUNT * 3 + i * 3 + 2] = BufTmpCoeffAcc[0][i].z;
        }
    }
}

